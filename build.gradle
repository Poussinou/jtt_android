// -*- Mode: Gradle; tab-width: 4; indent-tabs-mode: nil; -*-
// vim: et ts=4 sts=4 sw=4 syntax=groovy
buildscript {
    repositories {
        mavenCentral()
        jcenter()
    }
    dependencies {
        classpath 'com.android.tools.build:gradle:2.1.0'
    }
}

apply plugin: 'com.android.application'
apply plugin: 'jacoco'

repositories {
    mavenCentral()
}

configurations {
    unitTestCompile.extendsFrom runtime
    unitTestRuntime.extendsFrom unitTestCompile
}

dependencies {
    compile 'com.luckycatlabs:SunriseSunsetCalculator:1.1'
    compile 'com.android.support:support-v4:25.1.0'

    testCompile 'junit:junit:4.12'
    unitTestCompile files("$project.buildDir/intermediates/classes/debug")
    unitTestCompile 'junit:junit:4.12'
    unitTestCompile 'org.hamcrest:hamcrest-integration:1.3'
    unitTestCompile 'org.hamcrest:hamcrest-core:1.3'
    unitTestCompile 'org.hamcrest:hamcrest-library:1.3'
    unitTestCompile 'com.luckycatlabs:SunriseSunsetCalculator:1.1'
}

sourceSets {
    unitTest {
        java.srcDirs = ['src/test/java']
        resources.srcDirs = ['res']
    }
}

android {
    sourceSets {
        main {
            manifest.srcFile 'AndroidManifest.xml'
            java.srcDirs = ['src/main/java']
            res.srcDirs = ['res']
        }
        debug.setRoot('build-types/debug')
        release.setRoot('build-types/release')

        androidTest.setRoot('JttAndroidTest')
        androidTest {
           java.srcDirs = ['JttAndroidTest/src']
           res.srcDirs = ['JttAndroidTest/res']
        }
    }
    compileSdkVersion 'android-25'
    buildToolsVersion '25.0.2'

    defaultConfig {
        testApplicationId "com.aragaer.jtt.test"
        testInstrumentationRunner "android.test.InstrumentationTestRunner"
    }

    signingConfigs {
        release {
            storeFile file(System.getenv('HOME')+'/'+RELEASE_STORE_FILE)
            storePassword RELEASE_STORE_PASSWORD
            keyAlias RELEASE_KEY_ALIAS
            keyPassword RELEASE_KEY_PASSWORD
        }
    }

    buildTypes {
        debug {
            testCoverageEnabled true
        }
        release {
            minifyEnabled true
            proguardFile 'proguard.cfg'
            signingConfig signingConfigs.release
        }
    }
}

task unitTest(type: Test, dependsOn: assembleDebug) {
    description = "run unit tests"
    testClassesDir = project.sourceSets.unitTest.output.classesDir
    classpath = project.sourceSets.unitTest.runtimeClasspath
}

check.dependsOn unitTest

task jacocoTestReport(type: JacocoReport, dependsOn: "unitTest") {
    group = "Reporting"
    description = "Generate Jacoco coverage reports after running tests."
    reports {
        xml.enabled = true
        html.enabled = true
    }

    // Class R is used, but usage will not be covered, so ignore this class from report
    classDirectories = fileTree(
        dir: './build/intermediates/classes/debug',
        excludes: ['**/R.class',
                   '**/R$*.class'
    ])
    sourceDirectories = files('src/main/java')
    executionData = files('build/jacoco/unitTest.exec')
}
